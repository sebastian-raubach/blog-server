import com.bmuschko.gradle.cargo.convention.Deployable
import com.bmuschko.gradle.cargo.tasks.remote.CargoRedeployRemote

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-cargo-plugin:2.7.0'
    }
}

plugins {
    id 'java'
    id 'war'
}

apply plugin: 'com.bmuschko.cargo-base'

compileJava.options.encoding = 'UTF-8'

group 'uk.co.frickl'
version '1.0.0'

repositories {
    mavenCentral()
}

sourceCompatibility = 11

dependencies {
    providedCompile 'javax.servlet:javax.servlet-api:3.0.1'
    implementation 'org.glassfish.jersey.containers:jersey-container-grizzly2-servlet:2.34'
    implementation 'org.glassfish.jersey.inject:jersey-hk2:2.34'

    implementation 'commons-io:commons-io:2.6'

    implementation 'mysql:mysql-connector-java:8.0.25'
    implementation 'org.jooq:jooq:3.11.10'
    implementation 'org.jooq:jooq-codegen:3.11.10'

    implementation 'com.google.code.gson:gson:2.8.7'
    implementation 'org.json:json:20160212'

    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'javax.activation:activation:1.1.1'
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:2.3.3'
    implementation 'org.glassfish.jaxb:jaxb-runtime:2.3.3'
    implementation 'org.glassfish.jersey.media:jersey-media-jaxb:2.34'
    implementation 'org.glassfish.jersey.media:jersey-media-json-jackson:2.34'
    implementation 'org.glassfish.jersey.media:jersey-media-multipart:2.34'

    implementation 'com.drewnoakes:metadata-extractor:2.16.0'
    implementation 'net.coobird:thumbnailator:0.4.14'

    // CORS wizardry
    implementation 'com.thetransactioncompany:cors-filter:2.10'
}

// Runs the JOOQ code generation
task codegen (type: JavaExec) {
    group = 'Execution'
    classpath = sourceSets.main.runtimeClasspath
    main = 'org.jooq.codegen.GenerationTool'
    args 'jooq.xml'
    finalizedBy build
}

// Generate a .war file
war {
    dependsOn jar

    rootSpec.exclude('**/blog/**/*.class')
    rootSpec.includeEmptyDirs = false

    // Include external .jar files, but exclude the Germinate client and core.
    classpath fileTree(dir:'build/libs/', include:'*.jar')

    // Set the classpath
    manifest {
        attributes('Class-Path': configurations.runtimeClasspath.files.collect { it.getName() }.join(' '))
    }

    webInf {
        // Include the .properties file into the classes folder
        from(project.projectDir.toString()) {
            include 'config.properties'
            include 'logging.properties'
            into('classes')
        }
    }

    // Include the client code if it's available
    from("${project.projectDir.toString()}/client") {
        include '**/**.*'
        into('/')
    }
}

// Deploy the created .war file to Tomcat
task deployTomcat (type: CargoRedeployRemote) {
    dependsOn = [war]
    containerId = project.findProperty('tomcat.manager.version') ?: "tomcat8x"
    protocol = project.findProperty('tomcat.manager.protocol') ?: "http"
    hostname = project.findProperty('tomcat.manager.hostname') ?: "localhost"
    port = (project.findProperty('tomcat.manager.port') ?: "8080") as Integer
    username = project.findProperty('tomcat.manager.username') ?: ""
    password = project.findProperty('tomcat.manager.password') ?: ""
    deployables = [new Deployable(files: project.files([war.archiveFile]), context: "${project.'project.name'}/v${project.version}")]
}